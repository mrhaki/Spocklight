<html>
<head>
</head>
<body>

<h3 class="post-title">Support for Hamcrest Matchers</h3>

<div class="post">
<p>Spock has support for <a href="http://code.google.com/p/hamcrest/">Hamcrest</a> matchers and adds some extra syntactic sugar. In an <code>expect:</code> block in a Spock specification method we can use the following syntax <em>value Matcher</em>. Let's create a sample Spock specification and use this syntax with the Hamcrest matcher <code>hasKey</code>:</p><pre class="brush:groovy">// File: SampleSpecification.groovy
package com.mrhaki.spock

@Grab('org.hamcrest:hamcrest-all:1.3')
import static org.hamcrest.Matchers.*

@Grab('org.spockframework:spock-core:0.7-groovy-2.0')
import spock.lang.Specification

class SampleSpecification extends Specification {

    def "sample usage of hamcrest matcher hasKey"() {
        given:
        final sampleMap = [name: 'mrhaki']

        expect:
        sampleMap hasKey('name')
        sampleMap not(hasKey('name')) // To show assertion message.
    }

}
</pre><p>We can run the code (<code>$groovy SampleSpecification.groovy</code>) and see in the output a very useful assertion message for the second matcher in the <code>expect:</code> block. We directly see what went wrong and what was expected.</p><pre class="brush:plain">$ groovy SampleSpecification.groovy
JUnit 4 Runner, Tests: 1, Failures: 1, Time: 210
Test Failure: sample usage of hamcrest matcher hasKey(com.mrhaki.spock.SampleSpecification)
Condition not satisfied:

sampleMap not(hasKey('name'))
|         |
|         false
[name:mrhaki]

Expected: not map containing ["name"-&gt;ANYTHING]
     but: was &lt;{name=mrhaki}&gt;

 at com.mrhaki.spock.SampleSpecification.sample usage of hamcrest matcher hasKey(SampleSpecification.groovy:18)

</pre><p>With Spock we can rewrite the specification and use the static method <code>that()</code> in <code>spock.util.matcher.HamcrestSupport</code> as a shortcut for the Hamcrest <code>assertThat()</code> method. The following sample shows how we can use <code>that()</code>. With this method we can use the assertion outside an <code>expect:</code> or <code>then:</code> block.</p><pre class="brush:groovy">// File: SampleSpecification.groovy
package com.mrhaki.spock

@Grab('org.hamcrest:hamcrest-all:1.3')
import static org.hamcrest.Matchers.*

@Grab('org.spockframework:spock-core:0.7-groovy-2.0')
import static spock.util.matcher.HamcrestSupport.*
import spock.lang.Specification

class SampleSpecification extends Specification {

    def "sample usage of hamcrest matcher hasKey"() {
        given:
        final sampleMap = [name: 'mrhaki']

        expect:
        that sampleMap, hasKey('name')
    }

}
</pre><p>Finally we can use the <code>expect()</code> method in <code>spock.util.matcher.HamcrestSupport</code> to add the assertion in a <code>then:</code> block. This improves readability of our specification.</p><pre class="brush:groovy">// File: SampleSpecification.groovy
package com.mrhaki.spock

@Grab('org.hamcrest:hamcrest-all:1.3')
import static org.hamcrest.Matchers.*

@Grab('org.spockframework:spock-core:0.7-groovy-2.0')
import static spock.util.matcher.HamcrestSupport.*
import spock.lang.Specification

class SampleSpecification extends Specification {

    def "sample usage of hamcrest matcher hasKey"() {
        when:
        final sampleMap = [name: 'mrhaki']

        then:
        expect sampleMap, hasKey('name')
    }

}
</pre><p>Code written with Spock 0.7-groovy-2.0</p
</div>

<p>
    <a href="http://mrhaki.blogspot.com/2013/05/spocklight-support-for-hamcrest-matchers.html">Original blog post</a> written on May 14, 2013.
</p>

</body>
</html>