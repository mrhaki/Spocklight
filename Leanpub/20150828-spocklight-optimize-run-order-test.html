<html>
<head>
</head>
<body>

<h3 class="post-title">Optimize Run Order Test Methods </h3>

<div class="post">
<p>Spock is able to change the execution order of test methods in a specification. We can tell Spock to re-run failing methods before successful methods. And if we have multiple failing or successful tests, than first run the fastest methods, followed by the slower methods. This way when we re-run the specification we immediately see the failing methods and could stop the execution and fix the errors. We must set the property <code>optimizeRunOrder</code> in the <code>runner</code> configuration of the Spock configuration file. A Spock configuration file with the name <code>SpockConfig.groovy</code> can be placed in the classpath of our test execution or in our <code>USER_HOME/.spock</code> directory. We can also use the Java system property <code>spock.configuration</code> and assign the filename of our Spock configuration file.</p><p>In the following example we have a specification with different methods that can be successful or fail and have different durations when executed:</p><pre class="brush:groovy">package com.mrhaki.spock

import spock.lang.Specification
import spock.lang.Subject

class SampleSpec extends Specification {

    @Subject
    private Sample sample = new Sample()

    def "spec1 - slowly should return name property value"() {
        given:
        sample.name = testValue

        expect:
        sample.slowly() == testValue

        where:
        testValue = 'Spock rules'
    }

    def "spec2 - check name property"() {
        given:
        sample.name = testValue

        expect:
        sample.name == testValue

        where:
        testValue = 'Spock is gr8'
    }

    def "spec3 - purposely fail test at random"() {
        given:
        sample.name = testValues[randomIndex]

        expect:
        sample.name == testValues[0]

        where:
        testValues = ['Spock rules', 'Spock is gr8']
        randomIndex = new Random().nextInt(testValues.size())
    }

    def "spec4 - purposely fail test slowly"() {
        given:
        sample.name = 'Spock is gr8'

        expect:
        sample.slowly() == 'Spock rules'
    }

    def "spec5 - purposely fail test"() {
        given:
        sample.name = 'Spock rules'

        expect:
        sample.name == 'Spock is gr8'
    }
}

class Sample {
    String name

    String slowly() {
        Thread.sleep(2000)
        name
    }
}
</pre><p>Let's run our test where there is no optimised run order. We see the methods are executed as defined in the specification:</p><div class="separator" style="clear: both; text-align: center;"><a href="http://1.bp.blogspot.com/-wumjEe-wiUY/VeA3WxflGSI/AAAAAAAALOE/-jRIi5n_Q3o/s1600/spock-run-order-1.png" imageanchor="1" style="margin-left: 1em; margin-right: 1em;"><img border="0" src="http://1.bp.blogspot.com/-wumjEe-wiUY/VeA3WxflGSI/AAAAAAAALOE/-jRIi5n_Q3o/s400/spock-run-order-1.png" /></a></div><p style="clear:both;">Next we create a Spock configuration file with the following contents:</p><pre class="brush:groovy">runner {
    println "Optimize run order"
    optimizeRunOrder true
}
</pre><p>If we re-run our specification and have this file in the classpath we already see the order of the methods has changed. The failing tests are at the top and the successful tests are at the bottom. The slowest test method is last:</p><div class="separator" style="clear: both; text-align: center;"><a href="http://2.bp.blogspot.com/-tOMeqw6X0fg/VeA3ejVJ_WI/AAAAAAAALOM/mgNhtX2ntYE/s1600/spock-run-order-2.png" imageanchor="1" style="margin-left: 1em; margin-right: 1em;"><img border="0" src="http://2.bp.blogspot.com/-tOMeqw6X0fg/VeA3ejVJ_WI/AAAAAAAALOM/mgNhtX2ntYE/s400/spock-run-order-2.png" /></a></div><p style="clear:both;">Another re-run has optimised the order by running the slowest failing test after the other failing tests.</p><div class="separator" style="clear: both; text-align: center;"><a href="http://2.bp.blogspot.com/-MMHwzo8FjkI/VeA3iA6SWPI/AAAAAAAALOU/PeqNQu1EuZc/s1600/spock-run-order-3.png" imageanchor="1" style="margin-left: 1em; margin-right: 1em;"><img border="0" src="http://2.bp.blogspot.com/-MMHwzo8FjkI/VeA3iA6SWPI/AAAAAAAALOU/PeqNQu1EuZc/s400/spock-run-order-3.png" /></a></div><p style="clear:both;">Spock keeps track of the failing and successful methods and their execution time in a file with the specification name in the <code>USER_HOME/.spock/RunHistory</code> directory. To reset the information we must delete the file from this directory.</p><p>Written with Spock 1.0-groovy-2.4.</p
</div>

<p>
    <a href="http://mrhaki.blogspot.com/2015/08/spocklight-optimize-run-order-test.html">Original blog post</a> written on August 28, 2015.
</p>

</body>
</html>